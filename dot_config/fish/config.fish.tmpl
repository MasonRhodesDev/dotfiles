# Oh My Posh prompt - skip in Cursor integrated terminal
if not set -q TERM_PROGRAM; or test "$TERM_PROGRAM" != "vscode"
    oh-my-posh init fish --config {{ .chezmoi.homeDir }}/.config/oh-my-posh/bubbles.omp.json | source
end

# Vi keybindings (must come after oh-my-posh)
fish_vi_key_bindings

# Vi mode indicator for oh-my-posh
function rerender_on_bind_mode_change --on-variable fish_bind_mode
    if test "$fish_bind_mode" != paste -a "$fish_bind_mode" != "$FISH__BIND_MODE"
        set -gx FISH__BIND_MODE $fish_bind_mode
        omp_repaint_prompt
    end
end

function fish_default_mode_prompt --description "Display vi prompt mode"
    # Masked - oh-my-posh handles mode display
end

# Source uwsm environment variables
if test -f ~/.config/uwsm/env
    for line in (cat ~/.config/uwsm/env | grep -v '^#' | grep -v '^$')
        set -l clean_line (string replace 'export ' '' $line)
        set -l parts (string split '=' $clean_line)
        if test (count $parts) -eq 2
            set -gx $parts[1] $parts[2]
        end
    end
end

# Note: Most environment variables and PATH are set via conf.d/environment.fish
# which sources ~/.config/environment (shared with bash)
